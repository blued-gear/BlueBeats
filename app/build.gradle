plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'idea'
    id 'com.jaredsburrows.license' version '0.9.0'
}

idea{
    module{
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    google()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

android {
    signingConfigs {
        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())

            storeFile file(properties.getProperty("keystore.file"))
            storePassword properties.getProperty("keystore.password")
            keyAlias 'main'
            keyPassword properties.getProperty("keystore.password")
        }
    }

    compileSdkVersion version_sdk
    buildToolsVersion version_buildtools

    defaultConfig {
        applicationId "apps.chocolatecakecodes.bluebeats"
        minSdkVersion 25
        targetSdkVersion version_sdk
        versionCode 1
        versionName "Alpha-1.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/misc/db_schemas/".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += '-Xjvm-default=enable'
    }

    namespace 'apps.chocolatecakecodes.bluebeats'
}

dependencies {
    def room_version = "2.5.0"
    def fastadapter_version = '5.7.0'
    def media2_version = "1.2.1"
    def lifecycle_version = "2.5.1"

    implementation project(':taglib')

    implementation fileTree(dir: "${project.buildFile.parent}/libs/", include: '**/*.aar')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$version_kotlin"
    implementation "androidx.core:core-ktx:$version_android_ktx"

    implementation "androidx.appcompat:appcompat:$version_appcompat"
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'

    implementation 'com.anggrayudi:storage:1.5.4'
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation 'com.google.guava:guava:31.1-android'
    implementation "androidx.media2:media2-session:$media2_version"
    implementation "androidx.media2:media2-common:$media2_version"

    implementation "com.mikepenz:fastadapter:${fastadapter_version}"
    implementation "com.mikepenz:fastadapter-extensions-expandable:${fastadapter_version}"
    implementation "com.mikepenz:fastadapter-extensions-binding:${fastadapter_version}"
    implementation "com.mikepenz:fastadapter-extensions-diff:${fastadapter_version}"
    implementation "com.mikepenz:fastadapter-extensions-drag:${fastadapter_version}"
    implementation "com.mikepenz:fastadapter-extensions-scroll:${fastadapter_version}"
    implementation "com.mikepenz:fastadapter-extensions-ui:${fastadapter_version}"
    implementation "com.mikepenz:fastadapter-extensions-utils:${fastadapter_version}"

    implementation 'com.github.cachapa:ExpandableLayout:2.9.2'
    implementation 'io.noties.markwon:core:4.6.2'

    implementation 'org.videolan.android:libvlc-all:3.5.1'

    testImplementation "junit:junit:$version_junit"
    androidTestImplementation "androidx.test.ext:junit:$version_android_junit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$version_espresso"
    testImplementation "androidx.room:room-testing:$room_version"
}
